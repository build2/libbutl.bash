# file      : tests/manifest-parser/testscript
# copyright : Copyright (c) 2014-2019 Code Synthesis Ltd
# license   : MIT; see accompanying LICENSE file

: stdin
:
{
  $* <<EOI >>EOO
    : 1
    name: foo
    version: 1.2.3
    description: \
    foo
    executable
    \
    depends: libfoo
    depends: libbar
    EOI
    1

    name
    foo

    version
    1.2.3

    description
    foo
    executable

    depends
    libfoo

    depends
    libbar
    EOO
}

: file
:
{
  cat <<EOI >=file;
    : 1
    name: foo
    version: 1.2.3
    EOI

  $* file >>EOO
    1

    name
    foo

    version
    1.2.3
    EOO
}

: list
:
{
  $* <<EOI >>EOO
    : 1
    name: foo
    :
    version: 1.2.3
    EOI
    1

    name
    foo
    1

    version
    1.2.3
    EOO
}

: version
:
{
  : absent
  :
  $* <<EOI 2>'stdin:1:1: error: format version pair expected' != 0
    name: foo
    EOI

  : unsupported
  :
  $* <<EOI >- 2>'stdin:3:3: error: unsupported format version 2' != 0
    : 1
    name: foo
    : 2
    version: 1.2.3
    EOI
}

: empty
:
{
  $* <:'' >:''
}
